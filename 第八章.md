# 实验7

**关于除法在此题目中的应用：**

​    我们发现：被除数（总收入）是32位的，除数（雇员数）是16位的，那么在做除法时它们应该怎样存储？

​    被除数：低16位的存储在ax中，高16位的存储在dx中；那么

```assembly
（ax）= （bx+84）;

（dx） = （bx+86）。相应代码为：

  mov ax,[bx+84]

  mov dx,[bx+86]
  
   mov bp,[di+168]

   div bp

   mov es:[si+13],ax
```

​    除数:我们将除数存储在bp寄存器中吧，因为考虑以后有寄存器的冲突,当然我们亦可以使用堆栈，本例中使用了栈。     

### 版本一

```assembly
		;结构体定义
		;char year[4] // 年
		; 空格(1 Byte)
		;int income (4 Bytes)// 收入
		; 空格
		;empoyer num (2 Bytes) // 雇员数
		; 空格
		;人均收入 (2 Bytes)
		; 空格

		; 要求：将data段的数据拷贝进table段数据，并结构化如上述格式，然后计算21年的人均收入

		assume ds:data, es:table, cs:code, ss:stack

		data    segment 
			db    '1975', '1976', '1977', '1978', '1979', '1980', '1981', '1982', '1983' 
			db    '1984', '1985', '1986', '1987', '1988', '1989', '1990', '1991', '1992' 
			db    '1993', '1994', '1995'

			dd    16, 22, 382, 1356, 2390, 8000, 16000, 24486, 50065, 97479, 140417, 197514 
			dd    345980, 590827, 803530, 1183000, 1843000, 2759000, 3753000, 4649000, 5937000 

			dw    3, 7, 9, 13, 28, 38, 130, 220, 476, 778, 1001, 1442, 2258, 2793, 4037, 5635, 8226 
			dw    11542, 14430, 15257, 17800 
		data    ends 

		table    segment 
			db    21 dup ('year summ ne ?? ') 
		table    ends 

		; 弄个栈，没什么用
		; 就有时候用来腾出个寄存器
		stack	segment
			dw 2 dup(0)
		stack	ends

		code	segment

											
		;设置data段
start:	mov ax,data
		
		mov ds,ax
		
		;设置table段
		mov ax,table
		
		mov es,ax
		
		;设置堆栈段
		mov ax,stack
		mov ss,ax
		mov sp,16
		
		;初始化变址寄存器
		mov bx,0
		mov si,0
		mov di,0
		
		;循环21次
		mov cx,21
		
		;处理年
	s:	mov ax,ds:[bx]
		mov es:[si+0],ax
		mov ax,ds:[bx+2]
		mov es:[si+2],ax
		
		;空格填充
		mov al,32
		mov es:[si+4],al
		
		;处理收入
		mov ax,ds:[bx+84]
		mov es:[si+5],ax
		mov dx,ds:[bx+86]
		mov es:[si+7],ax
		
		;空格填充
		mov al,32
		mov es:[si+9],al
		
		;处理雇员
		mov ax,ds:[di+168]
		mov es:[si+0ah],ax																
		;空格填充
		mov al,32
		mov es:[si+0ch],al
		
		;处理平均收入
		mov ax,ds:[bx+84]
		mov dx,ds:[bx+86]
		push cx
		mov cx,ds:[di+168]
		div cx
		pop cx
		mov es:[si+0dh],ax
		
		;空格填充
		mov al,32
		mov es:[si+0fh],al
		
		add si,16
		add di,2
		add bx,4
		
		loop s
		
		mov   ax,4c00h   
		int   21h  

	code ends

	end start
```

**debug测试结果**

![image-20200509235413629](C:\Users\poplar\Desktop\assembly-learn\images\image-20200509235413629.png)

### 版本二

```assembly

; 要求：将data段的数据拷贝进table段数据，并结构化如上述格式，然后计算21年的人均收入

assume ds:data, es:table, cs:code, ss:stack

data segment


		db	'1975','1976','1977','1978','1979','1980','1981','1982','1983'
		db	'1984','1985','1986','1987','1988','1989','1990','1991','1992'
		db	'1993','1994','1995'
		;以上是表示21年的21个字符串 year


		dd	16,22,382,1356,2390,8000,16000,24486,50065,97479,140417,197514
		dd	345980,590827,803530,1183000,1843000,2759000,3753000,4649000,5937000
		;以上是表示21年公司总收入的21个dword数据	sum

		dw	3,7,9,13,28,38,130,220,476,778,1001,1442,2258,2793,4037,5635,8226
		dw	11542,14430,15257,17800

		;employee


data ends

table    segment 
	db    21 dup ('year summ ne ?? ') 
table    ends 

stack	segment
	db 128 dup(0)
stack	ends

code	segment
										
		;设置data段
start:	mov ax,data		
		mov ds,ax
		
		;设置table段
		mov ax,table		
		mov es,ax
		
		;设置堆栈段
		mov ax,stack
		mov ss,ax
		mov sp,128
		
		;初始化变址寄存器
		mov bx,168
		mov di,84
		mov si,0
		mov bp,0
		
		;循环21次
		mov cx,21
		
		;处理年
	s:	push ds:[si]
		pop es:[bp]
		push ds:[si+2]
		pop es:[bp+2]
		
		;处理收入
		mov ax,ds:[di]
		mov es:[bp+5],ax
		mov dx,ds:[di+2]
		mov es:[bp+7],dx
		
		;处理雇员
		push ds:[bx]
		pop es:[bp+0ah]																
		;处理平均收入，div内存的
		div word ptr ds:[bx]
		mov es:[bp+0dh],ax
		
		add si,4
		add bp,16
		add di,4
		add bx,2
		
		loop s
		
		mov   ax,4c00h   
		int   21h  

	code ends

	end start
```

![image-20200515181328845](C:\Users\poplar\Desktop\assembly-learn\images\image-20200515181328845.png)

### 总结：

采取合适的访问内存的方式去访问这些结构化的数据
我们发现这些经过结构化后的数据编写代码去访问它们的时候特别方便
这些数据被结构化了
数据是零散的我们可以将这些零散的数据结构化
偏移的思想
表格
思想的转变
不要去被动的接受数据将 这些数据结构化，为我们后面编写代码提供方便

数据组织 <--------> 代码组织