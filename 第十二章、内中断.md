# 内中断

### 概念

任何一个通用的CPU，比如8086， 都具备一种能力， 可以在执行完当前正在执行的指令之后，检测到从CPU外部发送过来的或内部产生的一种特殊信息，并且可以立即对所接收到的信息进行处理。这种特殊的信息，我们可以称其为:中断信息。中断的意思是
指，CPU不再接着(刚执行完的指令)向下执行，而是转去处理这个特殊信息。

### 检测点12.1

(1)用debug查看内存，情况如下：

`0000:0000 68 10 A7 00 8B 01 70 00-16 00 9D 03 8B 01 70 00`

**解释：**一个表项存放一个中断向量，也就是一个中断处理程序的入口地址，这个入口地址包括**段地址**和**偏移地址**，一个表项占两个字，高地址存放段地址，低地址存放偏移地址。

 计算中断服务入口地址的方法是：从内存地址为中断类型码`*4`和中断类型码`*4+2`的两个字单元中读取中断处理过程的入口地址设置IP和CS

   ```assembly
 0号：00A7：1068
 1号：0070：108B
 2号：039D：0016
 3号：0070：108B
   ```



   3号：IP读取3*4=12的两个bytes就是12和13两个bytes，CS读取3*4+2=14的两个bytes就是14和15两个bytes。
   则3号中断源对应的中断处理程序入口的地址为：**0070:018b**


(2)存储N号中断源对应的中断处理程序入口的偏移地址的内存单元的地址为： 4N

存储N号中断源对应的中断处理程序入口的段地址的内存单元的地址为：  4N+2

 **讲解：**8086系统在存储器的最低1KB区域（00000H~003FFH）建立一个中断向量表，存放256个中断类型的中断向量。这1024个单元被分成256组，每组包括4个字节单元，存储一个中断向量的段基址和段内偏移地址，高2个字节用於存放段基址，低两个字节用於存放段内偏移地址。

[来源](http://blog.sina.com.cn/s/blog_171daf8e00102xcow.html)

`rep movsb`完成的工作复很多，`DS:[SI]`字节内容赋值给 `ES:[DI]`，`SI加一`，`DI加一`(注制:方向标志为0是递增，否则递减)，`CX减一`，再判断CX决定是否继续，而这些百操作无须在程序里度写出来。而知`loop`相当麻烦，需要在程序里明显写出如何做。麻烦是麻烦了但也提供了最道大的灵活性。

### 实验12

```assembly
assume cs:code
code segment	
start:
	;将Do0程序代码复制至0:200处
	mov ax,cs
	mov ds,ax
	mov si,offset do0					;设置ds:si指向源地址
	
	mov ax,0
	mov es,ax
	mov di,200h							;设置es:di指向目的地址
	mov cx,offset do0end-offset do0		;设置cx为传输长度
	cld									;设置传输方向为正
	rep movsb
	;设置0号中断向量表指向0:200h
	mov word ptr es:[0*4],200h			;存放段地址
	mov word ptr es:[0*4+2],0h 			;存放偏移地址
	
	mov ax,4c00h
	int 21h

do0:
	jmp short do0start
	db "Divide error!"
do0start:	
	push ax				;保存现场
	push ds
	push si
	push di
	push cx	
	
	mov ax,cs
	mov ds,ax
	mov si,202h			;设置ds:si指向字符串

	
	mov ax,0b800h		;设置显存开始位置
	mov es,ax
	mov di,12*160+34*2	;设置显示位置
	
	mov cx,13			;复制字符串
s:
	mov al,ds:[si]
	mov es:[di],al
	mov byte ptr es:[di+1],02h
	inc si
	add di,2
	loop s
							
	pop cx				;恢复现场
	pop di
	pop si
	pop ds
	pop ax
	
	mov ax,4c00h 
	int 21h
	;正常应使用iret回到中断结束程序处，实验要求返回DOS，所以调用int 21h    
	
do0end:nop

code ends
end start
```

测试代码

```assembly
assume cs:code
code segment
start:
	mov ax,1000h
	mov bh,1
	div bh
code ends
end start
```

![image-20200513121719036](images\image-20200513121719036.png)